(*

Authors: Nishant Kumar.

Copyright:
Copyright (c) 2018 Microsoft Research
Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:
The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.
THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*)
(**************************)
def void MatMulCSF2D(int32_pl i, int32_pl j, int32_pl k, int32_al[i][j] A, int32_al[j][k] B, int32_al[i][k] C, int32_pl consSF){
	for i1=[0:i]{
		for i2=[0:k]{
			C[i1][i2] = 0;
			for i3=[0:j]{
				C[i1][i2] = C[i1][i2] +_al (A[i1][i3]*B[i3][i2]);
			};
			C[i1][i2] = C[i1][i2] >> consSF;
		};
	};
}

(**************************)
(* These loop implementations of convolution run faster with multithreading *)

def void Conv2DLoop(int32_pl N, int32_pl H, int32_pl W, int32_pl CI, 
				   int32_pl FH, int32_pl FW, int32_pl CO, 
				   int32_pl zPadHLeft, int32_pl zPadHRight, int32_pl zPadWLeft, int32_pl zPadWRight, 
				   int32_pl strideH, int32_pl strideW,
				   int32_pl outH, int32_pl outW, int32_pl G,
				   int32_al[N][H][W][CI] inputArr, 
				   int32_al[FH][FW][CI/G][CO] filterArr, 
				   int32_pl consSF,
				   int32_al[N][outH][outW][CO] outArr){

	int32_pl GIS = CI/G;
	int32_pl GOS = CO/G; 				   

	for n=[0:N]{
		for cog=[0:GOS]{
			for cig=[0:GIS]{
				for g=[0:G]{
					for h=[0:outH]{
						for w=[0:outW]{
							
							int32_al val = 0;
							int32_pl ci = GIS*g + cig;
							int32_pl co = GOS*g + cog;
							int32_pl curPosH = strideH*h-zPadHLeft;

							for fh=[0:FH]{
								int32_pl curPosW = strideW*w-zPadWLeft;

								for fw=[0:FW]{
										if( (curPosH >= 0) && (curPosW >= 0) && (curPosH < H) && (curPosW < W)){
											val = val +_al (inputArr[n][curPosH][curPosW][ci]*filterArr[fh][fw][(ci/G)][co]);
										};	

										curPosW = curPosW + 1;
									};
									curPosH = curPosH + 1;
								};
								
								outArr[n][h][w][co] = outArr[n][h][w][co] +_al (val >> consSF);		
							};	
						};
					};
			};
		};
	};					   
}

(**************************)
def void Conv3DLoop(int32_pl N, int32_pl D, int32_pl H, int32_pl W, int32_pl CI, 
				   int32_pl FD, int32_pl FH, int32_pl FW, int32_pl CO, 
				   int32_pl zPadDLeft, int32_pl zPadDRight,int32_pl zPadHLeft, int32_pl zPadHRight, int32_pl zPadWLeft, int32_pl zPadWRight, 
				   int32_pl strideD, int32_pl strideH, int32_pl strideW,
				   int32_pl outD, int32_pl outH, int32_pl outW, 
				   int32_al[N][D][H][W][CI] inputArr, 
				   int32_al[FD][FH][FW][CI][CO] filterArr, 
				   int32_pl consSF,
				   int32_al[N][outD][outH][outW][CO] outArr){

	for n=[0:N]{
		for co=[0:CO]{
			for d=[0:outD]{
				for h=[0:outH]{
					for w=[0:outW]{
						for ci=[0:CI]{
							int32_al val = 0;
							for fd=[d*strideD:d*strideD+FD]{
								for fh=[h*strideH:h*strideH+FH]{
										for fw=[w*strideW:w*strideW+FW]{
											int32_pl curPosD = fd-zPadDLeft;
											int32_pl curPosH = fh-zPadHLeft;
											int32_pl curPosW = fw-zPadWLeft;
											if( (curPosD >= 0) && (curPosH >= 0) && (curPosW >= 0) && (curPosD < D) && (curPosH < H) && (curPosW < W)){
												int32_pl curFilterPosD = fd-(d*strideD);
												int32_pl curFilterPosH = fh-(h*strideH);
												int32_pl curFilterPosW = fw-(w*strideW);
												val = val +_al (inputArr[n][curPosD][curPosH][curPosW][ci]*filterArr[curFilterPosD][curFilterPosH][curFilterPosW][ci][co]);
											};
										};
									};
								};	
							outArr[n][d][h][w][co] = outArr[n][d][h][w][co] +_al (val >> consSF);		
						};
					};
				};	
			};
		};
	};					   
}


(**************************)
def void ConvTranspose3DLoop(int32_pl N, int32_pl D, int32_pl H, int32_pl W, int32_pl CI, 
				   int32_pl FD, int32_pl FH, int32_pl FW, int32_pl CO, 
				   int32_pl zPadDLeft, int32_pl zPadDRight,int32_pl zPadHLeft, int32_pl zPadHRight, int32_pl zPadWLeft, int32_pl zPadWRight, 
				   int32_pl strideD, int32_pl strideH, int32_pl strideW,
				   int32_pl outD, int32_pl outH, int32_pl outW, 
				   int32_al[N][D][H][W][CI] inputArr, 
				   int32_al[FD][FH][FW][CO][CI] filterArr, 
				   int32_pl consSF,
				   int32_al[N][outD][outH][outW][CO] outArr){

	for n=[0:N]{
		for co=[0:CO]{
			for d=[0:outD]{
				for h=[0:outH]{
					for w=[0:outW]{
						for ci=[0:CI]{
							int32_al val = 0;
							for fd=[d:d+FD]{
								for fh=[h:h+FH]{
										for fw=[w:w+FW]{

											int32_pl curPosD = (fd-zPadDLeft)/strideD;
											int32_pl curPosH = (fh-zPadHLeft)/strideD;
											int32_pl curPosW = (fw-zPadWLeft)/strideD;
											
											if( (curPosD >= 0) && (curPosH >= 0) && (curPosW >= 0) && (curPosD < D) && (curPosH < H) && (curPosW < W) && ((fd-zPadDLeft)%strideD == 0) && ((fh-zPadHLeft)%strideH == 0) && ((fw-zPadWLeft)%strideW == 0)){

												int32_pl curFilterPosD = FD+d-fd-1;
												int32_pl curFilterPosH = FH+h-fh-1;
												int32_pl curFilterPosW = FW+w-fw-1;
												val = val +_al (inputArr[n][curPosD][curPosH][curPosW][ci]*filterArr[curFilterPosD][curFilterPosH][curFilterPosW][co][ci]);
											};
										};
									};
								};	
							outArr[n][d][h][w][co] = outArr[n][d][h][w][co] +_al (val >> consSF);		
						};
					};
				};	
			};
		};
	};			  
}


(**************************)
def void ArgMax1(int32_pl outArrS1, int32_pl inArrS1, int32_pl inArrS2, int32_al[inArrS1][inArrS2] inArr, int32_pl dim, int32_al[outArrS1] outArr){
	for od=[0:inArrS1]{
		int32_al maxi = inArr[od][0];
		int32_al maxiIdx = 0;
		for i=[0:inArrS2]{
			int32_pl iL = i;
			maxiIdx = (inArr[od][i] > maxi) ? iL : maxiIdx;
			maxi = (inArr[od][i] > maxi) ? inArr[od][i] : maxi;
		};
		outArr[od] = maxiIdx;
	};
}

def void ArgMax3(int32_pl outs1, int32_pl outs2, int32_pl outs3, 
			   int32_pl ins1, int32_pl ins2, int32_pl ins3, int32_pl ins4,
			   int32_al[ins1][ins2][ins3][ins4] inArr, int32_pl dim, int32_al[outs1][outs2][outs3] outArr)
{
	for iter1=[0:ins1]{
		for iter2=[0:ins2]{
			for iter3=[0:ins3]{
				int32_al maxi = inArr[iter1][iter2][iter3][0];
				int32_al maxiIdx = 0;
				for i=[0:ins4]{
					int32_pl iL = i;
					maxiIdx = (inArr[iter1][iter2][iter3][i] > maxi) ? iL : maxiIdx;
					maxi = (inArr[iter1][iter2][iter3][i] > maxi) ? inArr[iter1][iter2][iter3][i] : maxi;
				};
				outArr[iter1][iter2][iter3] = maxiIdx;
			}
		}
	}
}

(**************************)
def void Relu2(int32_pl s1, int32_pl s2, int32_al[s1][s2] inArr, int32_al[s1][s2] outArr){
	for i1=[0:s1]{
		for i2=[0:s2]{
			outArr[i1][i2] = (inArr[i1][i2] > 0 ? inArr[i1][i2] : 0);
		};
	};
}

def void Relu4(int32_pl s1, int32_pl s2, int32_pl s3, int32_pl s4, int32_al[s1][s2][s3][s4] inArr, int32_al[s1][s2][s3][s4] outArr){
	for i1=[0:s1]{
		for i2=[0:s2]{
			for i3=[0:s3]{
				for i4=[0:s4]{
					outArr[i1][i2][i3][i4] = (inArr[i1][i2][i3][i4] > 0 ? inArr[i1][i2][i3][i4] : 0);
				};
			};
		};
	};
}

def void Relu5(int32_pl s1, int32_pl s2, int32_pl s3, int32_pl s4, int32_pl s5, int32_al[s1][s2][s3][s4][s5] inArr, int32_al[s1][s2][s3][s4][s5] outArr){
        for i1=[0:s1]{
                for i2=[0:s2]{
                        for i3=[0:s3]{
                                for i4=[0:s4]{
                                        for i5=[0:s5]{
                                                outArr[i1][i2][i3][i4][i5] = (inArr[i1][i2][i3][i4][i5] > 0 ? inArr[i1][i2][i3][i4][i5] : 0);
                                        };
                                };
                        };
                };
        };
}

(**************************)
def void ElemWiseMul2(int32_pl a1, int32_pl a2, int32_pl b1, int32_pl b2, int32_pl s1, int32_pl s2, int32_al[a1][a2] A, int32_al[b1][b2] B, int32_al[s1][s2] outArr, int32_pl shrout){
  int32_pl aIdx1 = 0;
  int32_pl aIdx2 = 0;
  int32_pl bIdx1 = 0;
  int32_pl bIdx2 = 0;
  for i1=[0:s1]{
    aIdx1 = ((a1 == 1) ? 0 : i1);
    bIdx1 = ((b1 == 1) ? 0 : i1);
    for i2=[0:s2]{
      aIdx2 = ((a2 == 1) ? 0 : i2);
      bIdx2 = ((b2 == 1) ? 0 : i2);
      outArr[i1][i2] = ((A[aIdx1][aIdx2] * B[bIdx1][bIdx2]) >> shrout);
    };
  };
}

def void ElemWiseMul4(int32_pl a1, int32_pl a2, int32_pl a3, int32_pl a4, int32_pl b1, int32_pl b2, int32_pl b3, int32_pl b4, int32_pl s1, int32_pl s2, int32_pl s3, int32_pl s4, int32_al[a1][a2][a3][a4] A, int32_al[b1][b2][b3][b4] B, int32_al[s1][s2][s3][s4] outArr, int32_pl shrout){
  int32_pl aIdx1 = 0;
  int32_pl aIdx2 = 0;
  int32_pl aIdx3 = 0;
  int32_pl aIdx4 = 0;
  int32_pl bIdx1 = 0;
  int32_pl bIdx2 = 0;
  int32_pl bIdx3 = 0;
  int32_pl bIdx4 = 0;
  for i1=[0:s1]{
    aIdx1 = ((a1 == 1) ? 0 : i1);
    bIdx1 = ((b1 == 1) ? 0 : i1);
    for i2=[0:s2]{
      aIdx2 = ((a2 == 1) ? 0 : i2);
      bIdx2 = ((b2 == 1) ? 0 : i2);
      for i3=[0:s3]{
        aIdx3 = ((a3 == 1) ? 0 : i3);
        bIdx3 = ((b3 == 1) ? 0 : i3);
        for i4=[0:s4]{
          aIdx4 = ((a4 == 1) ? 0 : i4);
          bIdx4 = ((b4 == 1) ? 0 : i4);
          outArr[i1][i2][i3][i4] = ((A[aIdx1][aIdx2][aIdx3][aIdx4] * B[bIdx1][bIdx2][bIdx3][bIdx4]) >> shrout);
        };
      };
    };
  };
}

def void ElemWiseMul5(int32_pl a1, int32_pl a2, int32_pl a3, int32_pl a4, int32_pl a5, int32_pl b1, int32_pl b2, int32_pl b3, int32_pl b4, int32_pl b5, int32_pl s1, int32_pl s2, int32_pl s3, int32_pl s4, int32_pl s5, int32_al[a1][a2][a3][a4][a5] A, int32_al[b1][b2][b3][b4][b5] B, int32_al[s1][s2][s3][s4][s5] outArr, int32_pl shrout){
  int32_pl aIdx1 = 0;
  int32_pl aIdx2 = 0;
  int32_pl aIdx3 = 0;
  int32_pl aIdx4 = 0;
  int32_pl aIdx5 = 0;
  int32_pl bIdx1 = 0;
  int32_pl bIdx2 = 0;
  int32_pl bIdx3 = 0;
  int32_pl bIdx4 = 0;
  int32_pl bIdx5 = 0;
  for i1=[0:s1]{
    aIdx1 = ((a1 == 1) ? 0 : i1);
    bIdx1 = ((b1 == 1) ? 0 : i1);
    for i2=[0:s2]{
      aIdx2 = ((a2 == 1) ? 0 : i2);
      bIdx2 = ((b2 == 1) ? 0 : i2);
      for i3=[0:s3]{
        aIdx3 = ((a3 == 1) ? 0 : i3);
        bIdx3 = ((b3 == 1) ? 0 : i3);
        for i4=[0:s4]{
          aIdx4 = ((a4 == 1) ? 0 : i4);
          bIdx4 = ((b4 == 1) ? 0 : i4);
          for i5=[0:s5]{
            aIdx5 = ((a5 == 1) ? 0 : i5);
            bIdx5 = ((b5 == 1) ? 0 : i5);
            outArr[i1][i2][i3][i4][i5] = ((A[aIdx1][aIdx2][aIdx3][aIdx4][aIdx5] * B[bIdx1][bIdx2][bIdx3][bIdx4][bIdx5]) >> shrout);
          };
        };
      };
    };
  };
}

(**************************)
def void ElemWiseDiv2(int32_pl a1, int32_pl a2, int32_pl b1, int32_pl b2, int32_pl s1, int32_pl s2, int32_al[a1][a2] A, int32_al[b1][b2] B, int32_al[s1][s2] outArr, int32_pl shrout){
  int32_pl aIdx1 = 0;
  int32_pl aIdx2 = 0;
  int32_pl bIdx1 = 0;
  int32_pl bIdx2 = 0;
  for i1=[0:s1]{
    aIdx1 = ((a1 == 1) ? 0 : i1);
    bIdx1 = ((b1 == 1) ? 0 : i1);
    for i2=[0:s2]{
      aIdx2 = ((a2 == 1) ? 0 : i2);
      bIdx2 = ((b2 == 1) ? 0 : i2);
      outArr[i1][i2] = ((A[aIdx1][aIdx2] / B[bIdx1][bIdx2]) >> shrout);
    };
  };
}
(**************************)
def void Floor2(int32_pl s1, int32_pl s2, int32_al[s1][s2] inArr, int32_al[s1][s2] outArr, int32_pl curSF){
	for i1=[0:s1]{
		for i2=[0:s2]{
			int32_pl mask = ~((1<<curSF) - 1);
			outArr[i1][i2] = (inArr[i1][i2] & mask);
		};
	};
}


(**************************)
(* int32_al[N][H][W][C] input *)
def void MaxPool(int32_pl N, int32_pl H, int32_pl W, int32_pl C,
				  int32_pl ksizeH, int32_pl ksizeW,
				  int32_pl zPadHLeft, int32_pl zPadHRight, int32_pl zPadWLeft, int32_pl zPadWRight,
				  int32_pl strideH, int32_pl strideW,
				  int32_pl N1, int32_pl imgH, int32_pl imgW, int32_pl C1,
  				  int32_al[N1][imgH][imgW][C1] inArr, 
				  int32_al[N][H][W][C] outArr){
	
	for n=[0:N]{
		for c=[0:C]{
			int32_pl leftTopCornerH = 0 - zPadHLeft;
			int32_pl extremeRightBottomCornerH = imgH - 1 + zPadHRight;
			int32_pl ctH = 0;
			while((leftTopCornerH + ksizeH - 1) <= extremeRightBottomCornerH){
				int32_pl leftTopCornerW = 0 - zPadWLeft;
				int32_pl extremeRightBottomCornerW = imgW - 1 + zPadWRight;
				int32_pl ctW = 0;
				while((leftTopCornerW + ksizeW - 1) <= extremeRightBottomCornerW){

					int32_al maxi = 0;
					if ((((leftTopCornerH < 0) || (leftTopCornerH >= imgH)) || ((leftTopCornerW < 0) || (leftTopCornerW >= imgW)))){
						maxi = 0;
					}
					else{
						maxi = inArr[n][leftTopCornerH][leftTopCornerW][c];
					};
					for fh=[0:ksizeH]{
						for fw=[0:ksizeW]{
							int32_pl curPosH = leftTopCornerH + fh;
							int32_pl curPosW = leftTopCornerW + fw;
							int32_al temp = 0;
							if ((((curPosH < 0) || (curPosH >= imgH)) || ((curPosW < 0) || (curPosW >= imgW)))){
								temp = 0;
							}
							else{
								temp = inArr[n][curPosH][curPosW][c];
							};
							maxi = ((maxi < temp) ? temp : maxi);
						};
					};

					outArr[n][ctH][ctW][c] = maxi;
					leftTopCornerW = leftTopCornerW + strideW;
					ctW = ctW + 1;
				};

				leftTopCornerH = leftTopCornerH + strideH;
				ctH = ctH + 1;
			};
		};
	};

}


(**************************)
(* int32_al[N][H][W][C] input *)
def void AvgPool(int32_pl N, int32_pl H, int32_pl W, int32_pl C,
				  int32_pl ksizeH, int32_pl ksizeW,
				  int32_pl zPadHLeft, int32_pl zPadHRight, int32_pl zPadWLeft, int32_pl zPadWRight,
				  int32_pl strideH, int32_pl strideW,
				  int32_pl N1, int32_pl imgH, int32_pl imgW, int32_pl C1,
  				  int32_al[N1][imgH][imgW][C1] inArr, 
				  int32_al[N][H][W][C] outArr){

	int32_pl rows = N*C*H*W;
	int32_al[rows] filterAvg;

	int32_pl rowIdx = 0;
	for n=[0:N]{
		for c=[0:C]{
			int32_pl leftTopCornerH = 0 - zPadHLeft;
			int32_pl extremeRightBottomCornerH = imgH - 1 + zPadHRight;
			int32_pl ctH = 0;
			while((leftTopCornerH + ksizeH - 1) <= extremeRightBottomCornerH){
				int32_pl leftTopCornerW = 0 - zPadWLeft;
				int32_pl extremeRightBottomCornerW = imgW - 1 + zPadWRight;
				int32_pl ctW = 0;
				while((leftTopCornerW + ksizeW - 1) <= extremeRightBottomCornerW){

					int32_al curFilterSum = 0;
					for fh=[0:ksizeH]{
						for fw=[0:ksizeW]{
							int32_pl curPosH = leftTopCornerH + fh;
							int32_pl curPosW = leftTopCornerW + fw;
							int32_al temp = 0;
							if ((((curPosH < 0) || (curPosH >= imgH)) || ((curPosW < 0) || (curPosW >= imgW)))){
								temp = 0;
							}
							else{
								temp = inArr[n][curPosH][curPosW][c];
							};
							curFilterSum = curFilterSum + temp;
						};
					};

					int32_pl ksizeH64 = ksizeH;
					int32_pl ksizeW64 = ksizeW;
					int32_pl filterSz64 = (ksizeH64) * (ksizeW64);
					int32_al curFilterAvg = curFilterSum/filterSz64;
					filterAvg[rowIdx] = curFilterAvg;
					rowIdx = rowIdx + 1;

					leftTopCornerW = leftTopCornerW + strideW;
					ctW = ctW + 1;
				};

				leftTopCornerH = leftTopCornerH + strideH;
				ctH = ctH + 1;
			};
		};
	};

	for n=[0:N]{
		for c=[0:C]{
			for h=[0:H]{
				for w=[0:W]{
					outArr[n][h][w][c] = filterAvg[(n*C*H*W) + (c*H*W) + (h*W) + w];
				};
			};
		};
	};

}


(**************************)
def void FusedBatchNorm4411(int32_pl s1, int32_pl s2, int32_pl s3, int32_pl s4, int32_al[s1][s2][s3][s4] inArr, int32_al[s4] multArr, int32_al[s4] biasArr, int32_pl consSF, int32_al[s1][s2][s3][s4] outputArr){
	for i1=[0:s1]{
		for i2=[0:s2]{
			for i3=[0:s3]{
				for i4=[0:s4]{
					int32_al t1 = (inArr[i1][i2][i3][i4] *_al multArr[i4]);
					int32_al t2 = (t1 >> consSF);
					outputArr[i1][i2][i3][i4] = t2 + biasArr[i4];
				};
			};
		};
	};
}

def void FusedBatchNorm5511(int32_pl s1, int32_pl s2, int32_pl s3, int32_pl s4, int32_pl s5, int32_al[s1][s2][s3][s4][s5] inArr, int32_al[s5] multArr, int32_al[s5] biasArr, int32_pl consSF, int32_al[s1][s2][s3][s4][s5] outputArr){
	for i1=[0:s1]{
		for i2=[0:s2]{
			for i3=[0:s3]{
				for i4=[0:s4]{
						for i5=[0:s5]{
						int32_al t1 = (inArr[i1][i2][i3][i4][i5] *_al multArr[i5]);
						int32_al t2 = (t1 >> consSF);
						outputArr[i1][i2][i3][i4][i5] = t2 + biasArr[i5];
					};
				};
			};
		};
	};
}


(**************************)
def void ReduceMean24(int32_pl outS1, int32_pl outS2, 
					  int32_pl inS1, int32_pl inS2, int32_pl inS3, int32_pl inS4, 
					  int32_al[inS1][inS2][inS3][inS4] inputArr,
					  int32_pl[2] axes,
					  int32_al[outS1][outS2] outputArr
					  )
{
	for i1=[0:outS1]{
		for i2=[0:outS2]{
			int32_al summ = 0;
			for i=[0:inS2]{
				for j=[0:inS3]{
					summ = summ + inputArr[i1][i][j][i2];
				};
			};
			int32_pl numElem = inS2*inS3;
			summ = summ / numElem;
			outputArr[i1][i2] = summ;
		};
	};
}

(* This one is used for onnx compilation *)
def void ReduceMeanO24(int32_pl outS1, int32_pl outS2, 
					  int32_pl inS1, int32_pl inS2, int32_pl inS3, int32_pl inS4, 
					  int32_al[inS1][inS2][inS3][inS4] inputArr,
					  int32_pl axis1, int32_pl axis2,
					  int32_al[outS1][outS2] outputArr
					  )
{
	for i1=[0:outS1]{
		for i2=[0:outS2]{
			int32_al summ = 0;
			for i=[0:inS3]{
				for j=[0:inS4]{
					summ = summ + inputArr[i1][i2][i][j];
				};
			};
			int32_pl numElem = inS3*inS4;
			summ = summ / numElem;
			outputArr[i1][i2] = summ;
		};
	};
}

(**************************)
def void ClearMemSecret1(int32_pl s1, int32_al[s1] arr)
{
	return;
}

def void ClearMemSecret2(int32_pl s1, int32_pl s2, int32_al[s1][s2] arr)
{
	return;
}

def void ClearMemSecret3(int32_pl s1, int32_pl s2, int32_pl s3, int32_al[s1][s2][s3] arr)
{
	return;
}

def void ClearMemSecret4(int32_pl s1, int32_pl s2, int32_pl s3, int32_pl s4, int32_al[s1][s2][s3][s4] arr)
{
	return;
}

def void ClearMemSecret5(int32_pl s1, int32_pl s2, int32_pl s3, int32_pl s4, int32_pl s5, int32_al[s1][s2][s3][s4][s5] arr)
{
	return;
}

def void ClearMemPublic2(int32_pl s1, int32_pl s2, int32_pl[s1][s2] arr)
{
	return;
}

(**************************)
def void StartComputation()
{
	return;
}

def void EndComputation()
{
	return;
}